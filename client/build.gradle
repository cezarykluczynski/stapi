dependencies {
	compile project(':contract')

	compile(group: 'io.swagger', name: 'swagger-codegen-cli', version: '2.2.1') {
		exclude(module: 'org.slf4j')
		exclude(module: 'log4j')
		exclude(module: 'logback-classic')
	}
	compile group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5'
	compile group: 'com.squareup.okhttp', name: 'logging-interceptor', version: '2.7.5'
	compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
}

wsdl2java {
	encoding = 'utf-8'
	wsdlsToGenerate = [
			[
					'-wsdlLocation', 'classpath:v1/wsdl/series.wsdl',
					'-p','com.cezarykluczynski.stapi.client.v1.soap',
					'-autoNameResolution', "./contract/src/main/resources/v1/wsdl/series.wsdl"
			],
			[
					'-wsdlLocation', 'classpath:v1/wsdl/performer.wsdl',
					'-p','com.cezarykluczynski.stapi.client.v1.soap',
					'-autoNameResolution', "./contract/src/main/resources/v1/wsdl/performer.wsdl"
			]
	]
	generatedWsdlDir = file("$projectDir/build/generated/src/main/java")
	wsdlDir = file("$projectDir/src/main/resources")
}

task swaggerGenerate(type: Exec) {
	File file = project.configurations.compile.find {
		it.name.startsWith("swagger-codegen-cli-")
	}

	workingDir "../"

	commandLine "java", "-jar", "${file.absolutePath}",  "generate",
			"-l", "java",
			"-i", "contract/src/main/resources/v1/swagger/stapi.yaml",
			"-c", "contract/src/main/resources/v1/swagger/config.json",
			"-o", "./client/build/generated"
}

compileJava {
	dependsOn(wsdl2java, swaggerGenerate)
	source    += "build/generated/src/main/java/"
}
