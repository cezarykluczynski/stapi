plugins {
	id 'io.freefair.lombok'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	id("org.openapi.generator") version "6.4.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	implementation project(':contract')
	implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.10.0'
	implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '4.10.0'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
	implementation group: 'io.gsonfire', name: 'gson-fire', version: '1.8.5'
	implementation group: 'javax.ws.rs', name: 'jsr311-api', version: '1.1.1'
	implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
	implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
	testImplementation project(':util').sourceSets.test.output
}

apply plugin: 'org.openapi.generator'

jar.archiveBaseName = 'stapi-client'

openApiGenerate {
	generatorName = "java"
	generateModelTests = false
	generateModelDocumentation = false
	generateApiTests = false
	generateApiDocumentation = false
	inputSpec = "$rootDir/contract/src/main/resources/openapi/stapi.yaml".toString()
	outputDir = "$rootDir/client/".toString()
	apiPackage = "com.cezarykluczynski.stapi.client.rest.api"
	invokerPackage = "com.cezarykluczynski.stapi.client.rest.invoker"
	modelPackage = "com.cezarykluczynski.stapi.client.rest.model"
	configOptions = [
			dateLibrary: "java8"
	]
	httpUserAgent = "STAPI/java/$stapiVersion"
}

clean.doFirst {
	delete "${rootDir}/client/src/main/java/com/cezarykluczynski/stapi/client/rest/api/"
	delete "${rootDir}/client/src/main/java/com/cezarykluczynski/stapi/client/rest/invoker/"
	delete "${rootDir}/client/src/main/java/com/cezarykluczynski/stapi/client/rest/model/"
	delete "${rootDir}/client/.openapi-generator/"
}

compileJava {
	dependsOn('openApiGenerate')
}

processResources {
	dependsOn('openApiGenerate')
}

delombok {
	dependsOn('openApiGenerate')
}

codenarcTest {
	dependsOn('openApiGenerate')
}

task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
	dependsOn('openApiGenerate')
}

artifacts {
	archives javadocJar, sourcesJar
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			group = 'com.cezarykluczynski.stapi'
			artifactId = 'stapi-client'
			version = "$stapiVersion"
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			pom {
				name = 'STAPI Client'
				description = 'Official client library for STAPI, a Star Trek API'
				url = 'https://stapi.co/'
				packaging = 'jar'
				licenses {
					license {
						name = 'MIT'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						id = 'cezary.kluczynski'
						name = 'Cezary Kluczy≈Ñski'
						email = 'cezary.kluczynski@gmail.com'
					}
				}
				scm {
					connection = 'https://github.com/cezarykluczynski/stapi.git'
					developerConnection = 'git@github.com:cezarykluczynski/stapi.git'
					url = 'https://github.com/cezarykluczynski/stapi'
				}
				withXml {
					Node pomNode = asNode()
					Node dependencies = ((NodeList) pomNode.get("dependencies")).first()
					List<Node> children = dependencies.children()
					for (int i = children.size() - 1; i >= 0; i--) {
						Node childNode = (Node) children.get(i)
						List<Node> dependencyList = childNode.children()
						boolean doDelete = true
						for (int j = 0; j < dependencyList.size(); j++) {
							Node subChild = (Node) dependencyList.get(j)
							String subChildName = (String) subChild.name()
							if (subChildName.endsWith('artifactId')) {
								String value = (String) subChild.value()
								if (value.contains('okhttp') && !value.contains('logging-interceptor') || value.contains('gson') || value.contains('javax')) {
									doDelete = false
								}
							}
						}

						if (doDelete) {
							dependencies.remove(childNode)
						}
					}
				}
			}
		}
	}
	repositories {
		maven {
			url version.endsWith('SNAPSHOT')
					? "https://oss.sonatype.org/content/repositories/snapshots/"
					: "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username(project.findProperty("ossrhUsername") as String)
				password(project.findProperty("ossrhPassword") as String)
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}

javadoc {
	failOnError(false)
	options.addStringOption('Xdoclint:none', '-quiet')
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}
