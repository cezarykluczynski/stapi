import com.github.spotbugs.snom.SpotBugsTask
import groovy.io.FileType

buildscript {
	repositories {
		mavenCentral()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath group: 'org.zeroturnaround', name: 'zt-zip', version: ztZipVersion
		classpath group: 'org.apache.commons', name: 'commons-lang3', version: commonsLangVersion
		classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.14"
		classpath group: 'org.apache.groovy', name: 'groovy', version: groovyVersion
	}
}

plugins {
	id 'java'
	id 'io.freefair.lombok' version '8.0.1' apply false
	id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
	id "com.github.ben-manes.versions" version "0.46.0"
	id "io.spring.dependency-management" version "1.1.0"
}

allprojects {
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'jacoco'
	apply plugin: 'checkstyle'
	apply plugin: 'codenarc'
	apply plugin: 'com.github.spotbugs'

	compileJava.options.encoding = 'UTF-8'

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	tasks.withType(GroovyCompile) {
		groovyOptions.encoding = 'UTF-8'
	}

	tasks.withType(SpotBugsTask) {
		reports {
			html {
				required = true
				outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
				stylesheet = 'fancy-hist.xsl'
			}
		}
	}

	repositories {
		mavenCentral()
	}

	configurations.all {
		resolutionStrategy {
			// multiple versions
			force 'ch.qos.logback:logback-core:1.4.5'
			force 'ch.qos.logback:logback-classic:1.4.5'
			// multiple versions
			force "org.springframework.data:spring-data-jpa:$springDataVersion"
			// multiple versions
			force "net.bytebuddy:byte-buddy:$byteBuddyVersion"
		}
	}

	dependencies {
		implementation group: 'jakarta.platform', name: 'jakarta.jakartaee-web-api', version: '10.0.0'

		// Utils
		implementation group: 'org.json', name: 'json', version: '20230227'
		implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
		implementation group: 'org.apache.commons', name: 'commons-lang3', version: commonsLangVersion
		implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
		implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
		implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
		implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'
		implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.3'

		// Testing
		testImplementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
		testImplementation group: 'org.apache.groovy', name: 'groovy', version: groovyVersion
		testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
		testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: byteBuddyVersion
		testImplementation group: 'com.github.javaparser', name: 'javaparser-core', version: '3.25.2'
		testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
		testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.3'
	}

	test {
		useJUnitPlatform()
		maxParallelForks = 1
	}

	checkstyle {
		toolVersion = '10.9.3'
	}

	codenarc {
		configFile = "$rootDir/config/codenarc/codenarc.groovy" as File
		toolVersion = '3.2.0'
	}

	checkstyleMain {
		configProperties = [
				'baseDir': "$rootDir"
		]
	}

	spotbugs {
		toolVersion = '4.7.3'
		excludeFilter.set(file("$rootProject.projectDir/config/spotbugs/spotbugs-filter-exclude.xml"))
	}

	task cleanMetamodel(type: CleanMetamodel) {}

	clean {
		dependsOn cleanMetamodel
	}

	assemble.doFirst {
		def customProperties = 'application-stapi-custom.properties'
		File file = new File('server/src/main/resources/' + customProperties)
		if (!file.exists()) {
			logger.warn('Custom properties did not exists, empty file created.')
			file.createNewFile()
		}
	}
}

def isNonStable = { String version ->
	def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { stableKeyword -> version.containsIgnoreCase(stableKeyword) }
	if (stableKeyword) {
		return false
	}
	def unstableKeyword = ['-RC', '.CR', '-M', 'BETA', 'ALPHA'].any { unstableKeyword -> version.containsIgnoreCase(unstableKeyword) }
	return unstableKeyword
}

tasks.named("dependencyUpdates").configure {
	rejectVersionIf {
		isNonStable(it.candidate.version)
	}
}

nexusPublishing {
	repositories {
		sonatype {
			stagingProfileId = "6bc753ca200e26"
			username = project.findProperty("ossrhUsername") as String
			password = project.findProperty("ossrhPassword") as String
		}
	}
}

class CleanMetamodel extends DefaultTask {

	@TaskAction
	def cleanMetamodel() {
		def dir = new File('./model/src/main/generated/')
		dir.eachFileRecurse (FileType.FILES) { file ->
			if (file.name.endsWith("_.java")) {
				file.delete()
			}
		}
	}
}

apply from: 'testing.gradle'

group 'com.cezarykluczynski.stapi'
version "$stapiVersion"

sourceCompatibility = 17
targetCompatibility = 17
