import groovy.io.FileType

buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			url 'https://maven.atlassian.com/repository/public'
		}
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
		flatDir {
			dirs 'libs'
		}
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:' << springBootVersion
		classpath group: 'org.zeroturnaround', name: 'zt-zip', version: '1.11'
		classpath group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
		classpath group: 'gradle.plugin.com.github.spotbugs', name: 'gradlePlugin', version: '1.6.0'
		classpath "no.nils:wsdl2java:0.10"
	}
}

allprojects {
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'jacoco'
	apply plugin: 'no.nils.wsdl2java'
	apply plugin: 'checkstyle'
	apply plugin: 'codenarc'
	apply plugin: 'signing'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'com.github.spotbugs'

	compileJava.options.encoding = 'UTF-8'

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	tasks.withType(GroovyCompile) {
		groovyOptions.encoding = 'UTF-8'
	}

	tasks.withType(com.github.spotbugs.SpotBugsTask) {
		reports {
			xml.enabled = false
			html.enabled = true
		}
	}

	repositories {
		mavenCentral()
		jcenter()
		maven {
			url "http://maven.jahia.org/maven2/"
		}
	}

	dependencies {
		compile group: 'org.springframework.boot', name: 'spring-boot', version: springBootVersion
		compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: springBootVersion
		compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
		compile group: 'javax.inject', name: 'javax.inject', version: '1'

		// Utils
		compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
		compile group: 'org.json', name: 'json', version: '20160810'
		compile group: 'com.google.guava', name: 'guava', version: '20.0'
		compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
		compile group: 'commons-validator', name: 'commons-validator', version: '1.5.1'
		compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
		compile group: 'commons-validator', name: 'commons-validator', version: '1.5.1'
		compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'
		compile group: 'org.reflections', name: 'reflections', version: '0.9.11'
		compile group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '3.1.0'

		// Testing
		testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
		testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.10'
		testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4-rc-2'
		testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4-rc-2'
		testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion) {
			exclude(module: 'logback-classic')
			exclude(module: 'spring-boot-starter-logging')
		}
		testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion) {
			exclude(module: 'logback-classic')
			exclude(module: 'spring-boot-starter-tomcat')
			exclude(module: 'spring-boot-starter-logging')
		}
		testCompile group: 'cglib', name: 'cglib-nodep', version: '3.2.4'
		testCompile group: 'com.github.javaparser', name: 'javaparser-core', version: '3.2.0'
	}

	test {
		exclude '**/*IntegrationTest*'
	}

	checkstyle {
		configFile = "$rootDir/checkstyle.xml" as File
		toolVersion = '7.4'
	}

	codenarc {
		configFile = "$rootDir/codenarc.groovy" as File
		toolVersion = '0.26.0'
	}

	checkstyleMain {
		configProperties = [
				'baseDir': "$rootDir"
		]
	}

	spotbugs {
		toolVersion = '3.1.0'
		excludeFilter = file("$rootProject.projectDir/spotbugs-filter-exclude.xml")
	}

	task cleanMetamodel(type: CleanMetamodel) << {}

	clean {
		dependsOn cleanMetamodel
	}

	assemble.doFirst {
		def customProperties = 'application-stapi-custom.properties'
		File file = new File('server/src/main/resources/' + customProperties)
		if (!file.exists()) {
			logger.warn('Custom properties did not exists, empty file created.')
			file.createNewFile()
		}
	}

	configurations.codenarc {
		resolutionStrategy.eachDependency { DependencyResolveDetails d ->
			if (d.requested.group == 'org.codehaus.groovy') {
				d.useVersion '2.4.10'
			}
		}
	}
}

class CleanMetamodel extends DefaultTask {

	@TaskAction
	def cleanMetamodel() {
		def dir = new File('./model/src/main/java/')
		dir.eachFileRecurse (FileType.FILES) { file ->
			if (file.name.endsWith("_.java")) {
				file.delete()
			}
		}
	}
}

apply from: 'testing.gradle'

group 'com.cezarykluczynski.stapi'
version '0.0.14-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8
